# 文法

设计目的是让该文法可以通过LL(k)方法进行解析。

- L: 从左到右扫描（Left to right）
- L: 使用最左推导（Leftmost derivation）
- k: 有限地向前回溯k个符号（k token lookahead）

因为是最左推导，所以文法规则中需要对左递归进行消除（如果有）。

## 终结符

遇到终结符就不会继续推导，从而产生一个词元（Token）。终结符统一用大写字母表示。

- LET: 关键字 `let`
- CONST: 关键字 `const`
- MUT: 关键字 `mut`
- FN: 关键字 `fn`
- STRUCT: 关键字 `struct`
- TRAIT: 关键字 `trait`
- IMPL: 关键字 `impl`
- FOR: 关键字 `for`
- TYPE: 关键字 `type`
- IF: 关键字 `if`
- ELSE: 关键字 `else`
- LOOP: 关键字 `loop`
- WHILE: 关键字 `while`
- BREAK: 关键字 `break`
- CONTINUE: 关键字 `continue`
- IN: 关键字 `in`
- ENUM: 关键字 `enum`
- RANGE: `..`
- RANGEEQ: `..=`
- IDENT: `标识符`
- EQ: `=`
- LPAREN: `(`
- RPAREN: `)`
- LBRACE: `{`
- RBRACE: `}`
- SEMICOLON: `;`
- COLON: `:`
- COLONCOLON: `::`
- ARROW: `->`
- COMMA: `,`
- HASH: `#`
- DOT: `.`
- SQ: `'`
- DQ: `"`
- LBRACKET: `[`
- RBRACKET: `]`
- EOF: `文件结束`

- OR: `||` 或 关键词 `or`
- AND: `&&` 或 关键词 `and`
- NOT: `!` 或 关键词 `not`
- EQEQ: `==`
- NEQ: `!=`
- LT: `<`
- LTE: `<=`
- GT: `>`
- GTE: `>=`
- PLUS: `+`
- PLUSPLUS: `++`
- MINUS: `-`
- MINUSMINUS: `--`
- STAR: `*`
- STARSTAR: `**`
- SLASH: `/`
- COMMENT: `//`
- BITOR: `|`
- BITXOR: `^`
- BITAND: `&`
- BITINV: `~`

- INT: `整数字面量`
- FLOAT: `浮点数字面量`
- STRING: `字符串字面量`

- NEWLINE: `换行符`

- COMMENTVAL: `注释内容`

- SELF: `self` 仅在参数列表中的首位出现时有效，指向自身使用
- ATTRIBUTE_START: `#[`
- ASYNC: 关键词 `async`
- AWAIT: 关键词 `await`
- PUB: 关键词 `pub`
- CRATE: 关键词 `crate`
- SUPER: 关键词 `super`
- MOD: 关键词 `mod`
- AS: 关键词 `as`

## 非终结符

非终结符会继续向下推导直到所有符号均为终结符。非终结符开头字母大写，区别于终结符的全大写。见文法内的注释。

## 文法规则

### 基本文法

```
# 程序，文法开始
# 由任意个语句组成
Program -> Statement* EOF

# 语句
# 可以是 变量声明语句、函数声明语句、列表语句、结构体声明、Trait声明、实现声明、类型别名、枚举声明、控制流、表达式
Statement -> VarDecl | FnDecl | StructDecl | TraitDecl | ImplDecl | TypeAliasDecl | EnumDecl | ControlFlow | ExprStatement | ModuleDecl
# 表达式语句
ExprStatement -> Expr SEMICOLON

# 注释语句
CommentStatement -> COMMENT COMMENTVAL NEWLINE

# 变量声明
VarDecl -> Attributes (LET | CONST) MUT? IDENT (COLON TypeExpr)? EQ (Expr SEMICOLON | Block)

# 类型别名
TypeAliasDecl -> TYPE IDENT EQ TypeExpr SEMICOLON

# 结构体声明
StructDecl -> Attributes STRUCT IDENT Generics? (StructBody | SEMICOLON)
# 结构体块
StructBody -> LBRACE StructField? RBRACE
# 结构体字段列表 用于允许可选逗号
StructFieldList -> StructField (COMMA StructField)* COMMA?
# 结构体字段
StructField -> Attributes MUT? IDENT COLON TypeExpr (SEMICOLON | COMMA)

# Trait声明
TraitDecl -> Attributes TRAIT IDENT Generics? LBRACE TraitItem* RBRACE
# Trait块内部
TraitItem -> TraitFn | TypeAliasDecl SEMICOLON
# Trait 鸭子叫函数
TraitFn -> Attributes FN IDENT Generics? LPAREN Params RPAREN (ARROW TypeExpr)? (Block | SEMICOLON)

# 结构体实现声明 impl XXX {}
ImplDecl -> Attributes IMPL Generics? (TypeTrait FOR)? TypeExpr LBRACE ImplItem* RBRACE
# 实现声明块内部
ImplItem -> FnDecl | TypeAliasDecl SEMICOLON

# 函数声明
FnDecl -> Attributes Visibility? ASYNC? FN IDENT Generics? LPAREN Params RPAREN (ARROW TypeExpr)? Block

# 枚举声明
EnumDecl -> Attributes ENUM IDENT Generics? LBRACE EnumItemList? RBRACE
# 枚举项列表
EnumItemList -> EnumItem (COMMA EnumItem)* COMMA?
# 枚举项
EnumItem -> IDENT (LPAREN TypeExpr RPAREN)?

# 定义子模块
ModuleDecl -> Visibility? MOD IDENT;

# 属性注解
Attributes -> Attribute*
# 属性
Attribute -> ATTRIBUTE_START Namespace IDENT (LPAREN (IDENT | Literal) (COMMA (IDENT | Literal))* RPAREN)? RBRACKET

# 命名空间
Namespace -> (IDENT COLONCOLON)*

# 泛型
Generics -> LT GenericParam (COMMA GenericParam)* GT
# 泛型参数
GenericParam -> IDENT (COLON TypeTrait)?

# Trait类型 给impl XXXTrait for XXX {}用
TypeTrait -> IDENT (COLON COLON IDENT)*

# 参数列表
Params -> (Param (COMMA Param)* COMMA? )?

# 参数
Param -> IDENT COLON TypeExpr

# 代码块
Block -> LBRACE Statement* (Expr)? RBRACE

# 标签
Label -> SQ IDENT

# 可见性
Visibility -> PUB (LPAREN (SELF | SUPER | CRATE) RPAREN)?
```

### 表达式文法

表达式代表了一组**运算**。如果有点看不懂，记住这里是最左推导！不是最右！

```
# 表达式，顾名思义，它是要求值的并放在某个地方用的，或是需要其求值过程中的副作用
Expr -> Assignment

# 赋值表达式，允许连环复制
Assignment -> LogicalOr (EQ Assignment)?

# 逻辑或
LogicalOr -> LogicalAnd (OR LogicalAnd)*

# 逻辑与
LogicalAnd -> Equality (AND Equality)*

# 等式/不等式
Equality -> Relational ((EQEQ | NEQ) Relational)*

# 大小/相对关系
Relational -> Additive ((LT | LTE | GT | GTE) Additive)*

# 算术加减法
Additive -> Multiplicative ((PLUS | MINUS) Multiplicative)*

# 算术乘法
Multiplicative -> BitwiseOr ((STAR | SLASH) BitwiseOr)*

# 按位或
BitwiseOr -> BitwiseXor (BITOR BitwiseXor)*

# 按位异或
BitwiseXor -> BitwiseAnd (BITXOR BitwiseAnd)*

# 按位与
BitwiseAnd -> Exponential (BITAND Exponential)*

# 幂运算
Exponential -> Unary (STARSTAR Unary)*

# 一元运算符
Unary -> (MINUS | NOT | BITINV | PLUSPLUS | MINUSMINUS)? Primary (PLUSPLUS | MINUSMINUS)?

# 基本表达式
Primary -> IDENT | Literal | LPAREN Expr RPAREN | FnCall | ArrayInit | ControlFlow | BreakExpr | RangeExpr

# 字面量
Literal -> INT | FLOAT | STRING

# 函数调用
FnCall -> IDENT LPAREN ArgumentList RPAREN

# 参数列表
ArgumentList -> (Expr (COMMA Expr)*)?

# 数组初始化
ArrayInit -> LBRACKET Expr (COMMA Expr)* (SEMICOLON INT)? RBRACKET

# 类型成员访问
MemberAccess -> Primary (DOT IDENT | ARROW IDENT)*

# 类型
TypeExpr -> TupleTypeExpr | SimpleTypeExpr
# 基础类型
SimpleTypeExpr -> Namespace? IDENT Generics?
# 元组类型
TupleTypeExpr -> LPAREN TupleTypeItemList? RPAREN
# 元组元素列表
TupleTypeItemList -> TypeExpr (COMMA TypeExpr)* COMMA?

# 元组表达式
TupleExpr -> LPAREN TupleItemList? RPAREN
# 元组元素列表
TupleItemList -> TypeExpr (COMMA TypeExpr)* (COMMA)?
```

### 控制流文法

```
# 控制流
ControlFlow -> IfExpr | LoopExpr | WhileExpr | ForExpr

# if/else 表达式
IfExpr -> IF Expr Block (ELSE IF Expr Block)* (ELSE Block)?

# break 表达式
BreakExpr -> BREAK Label? Expr?

# loop 表达式
LoopExpr -> (Label COLON)? LOOP Block

# while 表达式
WhileExpr -> WHILE Expr Block

# for 表达式
ForExpr -> FOR IDENT IN Expr Block

# for range 表达式 （语法糖）
RangeExpr -> Expr RANGE Expr | Expr RANGE EQ Expr

```
