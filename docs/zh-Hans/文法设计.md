# 文法

设计目的是让该文法可以通过LL(k)方法进行解析。

- L: 从左到右扫描（Left to right）
- L: 使用最左推导（Leftmost derivation）
- k: 有限地向前回溯k个符号（k token lookahead）

因为是最左推导，所以文法规则中需要对左递归进行消除（如果有）。

## 终结符

LET: 关键字 `let`
CONST: 关键字 `const`
MUT: 关键字 `mut`
FN: 关键字 `fn`
IDENT: `标识符`
EQ: `=`
LPAREN: `(`
RPAREN: `)`
LBRACE: `{`
RBRACE: `}`
SEMICOLON: `;`
COLON: `:`
ARROW: `->`
COMMA: `,`
HASH: `#`
LBRACKET: `[`
RBRACKET: `]`
EXPR: `表达式（如1+1等）`
TYPE: `类型`
EOF: `文件结束`

OR: `||` 或 关键词 `or`
AND: `&&` 或 关键词 `and`
EQEQ: `==`
NEQ: `!=`
LT: `<`
LTE: `<=`
GT: `>`
GTE: `>=`
PLUS: `+`
MINUS: `-`
STAR: `*`
SLASH: `/`
NOT: `!` 或 关键词 `not`

INT: `整数字面量`
FLOAT: `浮点数字面量`
STRING: `字符串字面量`

## 非终结符

Program: `程序`
Statement: `语句`
VarDecl: `变量声明`
FnDecl: `函数声明`
Block: `代码块`
Params: `参数列表`
Param: `参数`

## 文法规则

### 基本文法

```
# 程序，文法开始
# 由任意个语句组成
Program -> Statement* EOF

# 语句
# 可以是 变量声明语句、函数声明语句、列表语句
Statement -> VarDecl | FnDecl | EXPR SEMICOLON

# 变量声明
VarDecl -> Attributes (LET | CONST) MUT? IDENT (COLON TYPE)? EQ (EXPR SEMICOLON | Block)

# 函数声明
FnDecl -> Attributes FN IDENT LPAREN Params RPAREN (ARROW TYPE)? Block

# 属性注解
Attributes -> Attribute*

# 属性
Attribute -> HASH LBRACKET Namespace IDENT (LPAREN (IDENT | Literal) (COMMA (IDENT | Literal))* RPAREN)? RBRACKET

# 命名空间
Namespace -> (IDENT COLON COLON)*

# 参数列表
Params -> (Param (COMMA Param)*)?

# 参数
Param -> IDENT COLON TYPE

# 代码块
Block -> LBRACE Statement* (EXPR)? RBRACE
```

### 表达式文法

表达式代表了一组**运算**。如果有点看不懂，记住这里是最左推导！不是最右！

```
# 表达式，顾名思义，它是要求值的并放在某个地方用的，或是需要其求值过程中的副作用
Expr -> Assignment

# 赋值表达式，允许连环复制
Assignment -> LogicalOr (EQ Assignment)?

# 逻辑或
LogicalOr -> LogicalAnd (OR LogicalAnd)*

# 逻辑与
LogicalAnd -> Equality (AND Equality)*

# 等式/不等式
Equality -> Relational ((EQEQ | NEQ) Relational)*

# 大小/相对关系
Relational -> Additive ((LT | LTE | GT | GTE) Additive)*

# 算术加法
Additive -> Multiplicative ((PLUS | MINUS) Multiplicative)*

# 算术乘法
Multiplicative -> Unary ((STAR | SLASH) Unary)*

# 一元运算符
Unary -> (MINUS | NOT)? Primary

# 基本表达式
Primary -> IDENT | Literal | LPAREN Expr RPAREN | FnCall

# 字面量
Literal -> INT | FLOAT | STRING

# 函数调用
FnCall -> IDENT LPAREN ArgumentList RPAREN

# 参数列表
ArgumentList -> (Expr (COMMA Expr)*)?
```
